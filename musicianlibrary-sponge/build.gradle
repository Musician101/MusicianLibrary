buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:spongegradle:0.8.1'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:vanillagradle:2.2-6'
    }
}

apply plugin: 'net.minecrell.vanillagradle.server'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.spongepowered.plugin'

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

minecraft {
    version = "1.12.2"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

dependencies {
    compile project(':musicianlibrary-common')
    compile 'org.spongepowered:spongeapi:8.0.0-SNAPSHOT'
    compile('org.spongepowered:mixin:0.7.1-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.sponge_musician_library.refmap.json"
    }
}

jar {
    manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.sponge_musician_library.json",
            "FMLCorePluginContainsFMLMod": "true"
    )
}

processResources {
    from('src/main/resources') {
        expand 'version': project.version
    }
}

shadowJar {
    dependencies {
        include(dependency(':musicianlibrary-common'))
    }
}
